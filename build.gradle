buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://cdn.fox2code.com/maven' }
    }
    dependencies {
        classpath('com.fox2code.FoxLoader:dev:2.0-alpha38')
    }
}

apply plugin: 'foxloader.dev'

group = 'net.tracystacktrace'
version = '0.4.0'

sourceSets {
    main {
        java {
            srcDirs = ['stackem-main/src']
        }
        resources {
            srcDirs = ['stackem-main/resources']
        }
    }

    toolsJson {
        java {
            srcDirs = ['tools-jsonparser/src']
        }
        resources {
            srcDirs = ['tools-jsonparser/resources']
        }
    }

    neptune {
        java {
            srcDirs = ['stackem-neptune/src']
        }
        resources {
            srcDirs = ['stackem-neptune/resources']
        }
        compileClasspath += sourceSets.toolsJson.output
        runtimeClasspath += sourceSets.toolsJson.output
    }

    sagittarius {
        java {
            srcDirs = ['stackem-sagittarius/src']
        }
        resources {
            srcDirs = ['stackem-sagittarius/resources']
        }
        compileClasspath += sourceSets.toolsJson.output
        runtimeClasspath += sourceSets.toolsJson.output
    }

    main {
        compileClasspath += sourceSets.toolsJson.output
        runtimeClasspath += sourceSets.toolsJson.output
        compileClasspath += sourceSets.neptune.output
        runtimeClasspath += sourceSets.neptune.output
        compileClasspath += sourceSets.sagittarius.output
        runtimeClasspath += sourceSets.sagittarius.output
    }
}

configurations {
    toolsJsonImplementation.extendsFrom implementation
    toolsJsonRuntimeOnly.extendsFrom runtimeOnly
    neptuneImplementation.extendsFrom implementation
    neptuneRuntimeOnly.extendsFrom runtimeOnly
    sagittariusImplementation.extendsFrom implementation
    sagittariusRuntimeOnly.extendsFrom runtimeOnly
}

dependencies {
    api 'org.semver4j:semver4j:5.3.0'
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

jar {
    from sourceSets.toolsJson.output
    from sourceSets.neptune.output
    from sourceSets.sagittarius.output

    from sourceSets.toolsJson.resources
    from sourceSets.neptune.resources
    from sourceSets.sagittarius.resources

    from "LICENSE"
}

foxloader {
    modId = "stackem"
    modName = "Stack 'Em!"
    modIcon = "assets/stackem/icon.png"
    modVersion = project.version
    modAuthors = "tracystacktrace"
    modMain = "net.tracystacktrace.stackem.StackEm"
    modWebsite = "https://github.com/tracystacktrace/StackEm"
    modDesc("§eStacked texturepacks, §dTexture gluing, §bItem textures swap!")
    modDesc()
    modDesc("\u00A76License: \u00A7aApache License 2.0")
    modDesc("\u00A76Sources: \u00A79https://github.com/tracystacktrace/StackEm")
}

tasks.withType(Javadoc).configureEach {
    source += sourceSets.toolsJson.allJava
    classpath += sourceSets.toolsJson.output + sourceSets.toolsJson.compileClasspath

    source += sourceSets.neptune.allJava
    classpath += sourceSets.neptune.output + sourceSets.neptune.compileClasspath

    source += sourceSets.sagittarius.allJava
    classpath += sourceSets.sagittarius.output + sourceSets.sagittarius.compileClasspath
}
