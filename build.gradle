buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://cdn.fox2code.com/maven' }
    }
    dependencies {
        classpath('com.fox2code.FoxLoader:dev:2.0-alpha38')
    }
}

apply plugin: 'foxloader.dev'

group = 'net.tracystacktrace'
version = '0.3.8'

sourceSets {
    main {
        java {
            srcDirs = ['stackem-main/src']
        }
        resources {
            srcDirs = ['stackem-main/resources']
        }
    }

    tools {
        java {
            srcDirs = ['stackem-main/src-tools']
        }
        resources {
            srcDirs = ['stackem-main/resources-tools']
        }
    }

    neptune {
        java {
            srcDirs = ['stackem-neptune/src']
        }
        resources {
            srcDirs = ['stackem-neptune/resources']
        }
        compileClasspath += sourceSets.tools.output
        runtimeClasspath += sourceSets.tools.output
    }

    sagittarius {
        java {
            srcDirs = ['stackem-sagittarius/src']
        }
        resources {
            srcDirs = ['stackem-sagittarius/resources']
        }
        compileClasspath += sourceSets.tools.output
        runtimeClasspath += sourceSets.tools.output
    }

    main {
        compileClasspath += sourceSets.tools.output
        runtimeClasspath += sourceSets.tools.output
        compileClasspath += sourceSets.neptune.output
        runtimeClasspath += sourceSets.neptune.output
        compileClasspath += sourceSets.sagittarius.output
        runtimeClasspath += sourceSets.sagittarius.output
    }
}

configurations {
    toolsImplementation.extendsFrom implementation
    toolsRuntimeOnly.extendsFrom runtimeOnly
    neptuneImplementation.extendsFrom implementation
    neptuneRuntimeOnly.extendsFrom runtimeOnly
    sagittariusImplementation.extendsFrom implementation
    sagittariusRuntimeOnly.extendsFrom runtimeOnly
}

dependencies {
    api 'org.semver4j:semver4j:5.3.0'
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

jar {
    from sourceSets.tools.output
    from sourceSets.neptune.output
    from sourceSets.sagittarius.output

    from sourceSets.tools.resources
    from sourceSets.neptune.resources
    from sourceSets.sagittarius.resources

    from "LICENSE"
}

foxloader {
    modId = "stackem"
    modName = "Stack 'Em!"
    modIcon = "assets/stackem/icon.png"
    modVersion = project.version
    modAuthors = "tracystacktrace"
    modMain = "net.tracystacktrace.stackem.StackEm"
    modWebsite = "https://github.com/tracystacktrace/StackEm"
    modDesc("§eStacked texturepacks, §dTexture gluing, §bItem textures swap!")
    modDesc()
    modDesc("\u00A76License: \u00A7aApache License 2.0")
    modDesc("\u00A76Sources: \u00A79https://github.com/tracystacktrace/StackEm")
}

tasks.withType(Javadoc).configureEach {
    source += sourceSets.tools.allJava
    classpath += sourceSets.tools.output + sourceSets.tools.compileClasspath

    source += sourceSets.neptune.allJava
    classpath += sourceSets.neptune.output + sourceSets.neptune.compileClasspath

    source += sourceSets.sagittarius.allJava
    classpath += sourceSets.sagittarius.output + sourceSets.sagittarius.compileClasspath
}
